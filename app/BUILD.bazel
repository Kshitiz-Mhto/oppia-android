# TODO(#1532): Rename file to 'BUILD' post-Gradle.
"""
This library contains the app's core user-facing source files and functionality.
"""

load("@tools_android//tools/crashlytics:defs.bzl", "crashlytics_android_library")
load("@tools_android//tools/googleservices:defs.bzl", "google_services_xml")
load("//defs:oppia_android_library.bzl", "oppia_android_resource_library")

filegroup(
    name = "layout_resources",
    srcs = glob(["src/main/res/layout*/**"]),
    visibility = ["//app/src/main/java/org/oppia/android/app/databinding:__pkg__"],
)

filegroup(
    name = "non_layout_resources",
    srcs = glob(
        ["src/main/res/**"],
        exclude = ["src/main/res/layout*/**"],
    ),
)

# Visibility to all app targets.
package_group(
    name = "app_prod_visibility",
    includes = [":app_testing_visibility"],
    packages = ["//app/src/main/..."],
)

# Visibility to app tests and test libraries.
package_group(
    name = "app_testing_visibility",
    packages = [
        "//app/src/main/java/org/oppia/android/app/notice/testing",
        "//app/src/main/java/org/oppia/android/app/player/exploration/testing",
        "//app/src/main/java/org/oppia/android/app/player/state/testing",
        "//app/src/main/java/org/oppia/android/app/testing",
        "//app/src/sharedTest/...",
        "//app/src/test/...",
    ],
)

filegroup(
    name = "manifest",
    srcs = ["src/main/AndroidManifest.xml"],
    visibility = [
        "//:oppia_binary_visibility",
        "//app/src/main/java/org/oppia/android/app/databinding:__pkg__",
    ],
)

# Library for non-layout resource files.
oppia_android_resource_library(
    name = "resources",
    resource_files = [":non_layout_resources"],
    visibility = [
        ":app_prod_visibility",
        "//:oppia_binary_visibility",
        "//:oppia_testing_visibility",
        "//instrumentation:__pkg__",
    ],
    deps = ["//third_party:com_google_android_material_material"],
)

# TODO(#1566): Move Firebase rules to their own package & remove default visibility
# Package for all Firebase dependencies.
# To reference these dependencies, add '//app:crashlytics' and '//app:crashlytics_deps'
# to your build rule's dependency list.

crashlytics_android_library(
    name = "_crashlytics",
    package_name = "org.oppia.android",
    build_id = "48fc9d17-e102-444c-8e0d-638d75ec0942",
    resource_files = google_services_xml(
        package_name = "org.oppia.android",
        google_services_json = "google-services.json",
    ),
)

android_library(
    name = "crashlytics_deps",
    visibility = [
        "//:oppia_binary_visibility",
        "//:oppia_testing_visibility",
        "//instrumentation:__pkg__",
        "//utility/src/main/java/org/oppia/android/util/logging/firebase:__pkg__",
    ],
    exports = [
        ":_crashlytics",
        "//third_party:com_google_firebase_firebase-crashlytics",
    ],
)

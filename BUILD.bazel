# TODO(#1532): Rename file to 'BUILD' post-Gradle.

load("@dagger//:workspace_defs.bzl", "dagger_rules")
load("//defs:build_flavors.bzl", "AVAILABLE_FLAVORS", "COMMON_RUNTIME_DEPS", "FLAVOR_METADATA", "define_oppia_aab_binary_flavor", "transform_android_manifest")
load("//defs:version.bzl", "MAJOR_VERSION", "MINOR_VERSION")

# Corresponds to being accessible to all Oppia targets. This should be used for production APIs &
# modules that may be used both in production targets and in tests.
package_group(
    name = "oppia_api_visibility",
    packages = [
        "-//scripts/...",
        "-//third_party/...",
        "//...",
    ],
)

# Corresponds to being accessible to the Oppia binary. This should only be used by production-facing
# modules that must be included in the top-level binary in order for the app to build.
package_group(
    name = "oppia_binary_visibility",
    packages = [
        "//",
    ],
)

# Corresponds to being accessible to Oppia tests. This should be used by fakes & test-only modules
# that can be included in tests for custom state arrangement or to satisfy upstream/downstream
# dependency requirements for components whose production implementations cannot be used in test
# environments.
# TODO(#2773): Remove the open visibility access granted here & instead restrict this access to only
# test targets.
package_group(
    name = "oppia_testing_visibility",
    includes = [
        "//app:app_testing_visibility",
        "//data:data_testing_visibility",
        "//domain:domain_testing_visibility",
        "//scripts:oppia_script_test_visibility",
        "//testing:testing_testing_visibility",
        "//utility:utility_testing_visibility",
    ],
    packages = [
        "//data/...",
        "//instrumentation/...",
        "//testing/...",
    ],
)

package_group(
    name = "oppia_e2e_testing_visibility",
    packages = [
        "//instrumentation/...",
    ],
)

# Special visibility group specific to debug mode. This provides access to the target for both the
# Oppia tests and specific debug only packages.
package_group(
    name = "oppia_debug_visibility",
    includes = [
        ":oppia_testing_visibility",
    ],
    packages = [
        "//app/src/main/java/org/oppia/android/app/devoptions/...",
    ],
)

# Special visibility group specific to prod modules. This provides access to the module for both the
# Oppia binary & tests.
package_group(
    name = "oppia_prod_module_visibility",
    includes = [
        ":oppia_binary_visibility",
        ":oppia_testing_visibility",
    ],
    packages = [
        "//app/src/main/java/org/oppia/android/app/application/...",
    ],
)

_APK_METADATA = {
    "oppia": FLAVOR_METADATA["dev"],
    "oppia_kitkat": FLAVOR_METADATA["dev_kitkat"],
}

# TODO(#1640): Move binary manifest to top-level package post-Gradle.
[
    transform_android_manifest(
        name = "oppia_apk_%s_transformed_manifest" % flavor,
        application_relative_qualified_class = ".app.application.dev.DeveloperOppiaApplication",
        build_flavor = flavor,
        input_file = "//app:manifest",
        major_version = MAJOR_VERSION,
        minor_version = MINOR_VERSION,
        output_file = "AndroidManifest_transformed_%s.xml" % flavor,
        version_code = apk_flavor_metadata["version_code"],
    )
    for (flavor, apk_flavor_metadata) in _APK_METADATA.items()
]

[
    android_binary(
        name = flavor,
        custom_package = "org.oppia.android",
        main_dex_list = apk_flavor_metadata.get("main_dex_list"),
        manifest = "oppia_apk_%s_transformed_manifest" % flavor,
        manifest_values = {
            "applicationId": "org.oppia.android",
            "minSdkVersion": "%d" % apk_flavor_metadata["min_sdk_version"],
            "targetSdkVersion": "%d" % apk_flavor_metadata["target_sdk_version"],
        },
        multidex = apk_flavor_metadata["multidex"],
        deps = apk_flavor_metadata["deps"] + COMMON_RUNTIME_DEPS,
    )
    for (flavor, apk_flavor_metadata) in _APK_METADATA.items()
]

# Define all binary flavors that can be built. Note that these are AABs, not APKs, and can be
# be installed on a local device or emulator using a 'bazel run' command like so:
#  bazel run //:install_oppia_dev
[
    define_oppia_aab_binary_flavor(name = flavor)
    for flavor in AVAILABLE_FLAVORS
]

dagger_rules(repo_name = "@maven_app")
